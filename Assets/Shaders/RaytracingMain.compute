
static const float PI = 3.14159265f;
static const float EPSILON = 1e-8;
static const float GOLDEN_RATIO = 1.61803;

#define mod(x, y) (x - y * floor(x / y))

struct Ray {
    float3 origin;
    float3 direction;
    float3 energy;
};

struct RayHit {
    float3 position;
    float distance;
    float3 normal;
    Material material;
};

struct Sphere {
    float3 position;
    float radius;
    Material material;
};

struct MeshObject {
    float4x4 localToWorldMatrix;
    int indices_offset;
    int indices_count;
    Material material;
};


RWTexture2D<float4> Result;
float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
int _ReflectionCount;
float _Time;

StructuredBuffer<Sphere> _Spheres;
StructuredBuffer<MeshObject> _MeshObjects;
StructuredBuffer<float3> _Vertices;
StructuredBuffer<int> _Indices;

Texture2D<float4> _SkyboxTexture;
Texture2D<float4> _NoiseTexture;
SamplerState sampler_SkyboxTexture;
SamplerState sampler_NoiseTexture;
float2 _PixelOffset;
float2 _Pixel;
float _Seed;

float3 Shade(inout Ray ray, RayHit hit);
void OnObjectHit(inout RayHit hit, Material material);
Material CreateMaterial();

float rand() {
    float result = frac(sin(_Seed / 100.0f * dot(_Pixel, float2(12.9898f, 78.233f))) * 43758.5453f);
    _Seed += 1.0f;
    return result;
}

float sdot(float3 x, float3 y, float f = 1.0f) {
    return saturate(dot(x, y) * f);
}

float energy(float3 color) {
    return dot(color, 1.0f / 3.0f);
}

RayHit CreateRayHit() {
    RayHit hit;
    hit.position = float3(0.0f, 0.0f, 0.0f);
    hit.distance = 1.#INF;
    hit.normal = 0.0f;
    hit.material = CreateMaterial();
    return hit;
}

Ray CreateRay(float3 origin, float3 direction) {
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    ray.energy = float3(1.0f, 1.0f, 1.0f);
    return ray;
}

Ray CreateCameraRay(float2 uv) {
    float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    float3 direction = mul(_CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
    direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);
    return CreateRay(origin, direction);
}

bool IntersectTriangle_MT97(Ray ray, float3 vert0, float3 vert1, float3 vert2,
    inout float t, inout float u, inout float v) {
    // find vectors for two edges sharing vert0
    float3 edge1 = vert1 - vert0;
    float3 edge2 = vert2 - vert0;
    // begin calculating determinant - also used to calculate U parameter
    float3 pvec = cross(ray.direction, edge2);
    // if determinant is near zero, ray lies in plane of triangle
    float det = dot(edge1, pvec);
    // use backface culling
    if (det < EPSILON)
        return false;
    float inv_det = 1.0f / det;
    // calculate distance from vert0 to ray origin
    float3 tvec = ray.origin - vert0;
    // calculate U parameter and test bounds
    u = dot(tvec, pvec) * inv_det;
    if (u < 0.0 || u > 1.0f)
        return false;
    // prepare to test V parameter
    float3 qvec = cross(tvec, edge1);
    // calculate V parameter and test bounds
    v = dot(ray.direction, qvec) * inv_det;
    if (v < 0.0 || u + v > 1.0f)
        return false;
    // calculate t, ray intersects triangle
    t = dot(edge2, qvec) * inv_det;
    return true;
}

void IntersectMeshObject(Ray ray, inout RayHit bestHit, MeshObject meshObject) {
    uint offset = meshObject.indices_offset;
    uint count = offset + meshObject.indices_count;
    for (uint i = offset; i < count; i += 3) {
        float3 v0 = (mul(meshObject.localToWorldMatrix, float4(_Vertices[_Indices[i]], 1))).xyz;
        float3 v1 = (mul(meshObject.localToWorldMatrix, float4(_Vertices[_Indices[i + 1]], 1))).xyz;
        float3 v2 = (mul(meshObject.localToWorldMatrix, float4(_Vertices[_Indices[i + 2]], 1))).xyz;
        float t, u, v;
        if (IntersectTriangle_MT97(ray, v0, v1, v2, t, u, v)) {
            if (t > 0 && t < bestHit.distance) {
                bestHit.distance = t;
                bestHit.position = ray.origin + t * ray.direction;
                bestHit.normal = normalize(cross(v1 - v0, v2 - v0));
                OnObjectHit(bestHit, meshObject.material);
            }
        }
    }
}

void IntersectSphere(Ray ray, inout RayHit bestHit, Sphere sphere) {
    // Calculate distance along the ray where the sphere is intersected
    float3 d = ray.origin - sphere.position;
    float p1 = -dot(ray.direction, d);
    float p2sqr = p1 * p1 - dot(d, d) + sphere.radius * sphere.radius;
    if (p2sqr < 0)
        return;
    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < bestHit.distance) {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = normalize(bestHit.position - sphere.position);
        OnObjectHit(bestHit, sphere.material);
    }
}

RayHit Trace(Ray ray) {

    RayHit bestHit = CreateRayHit();
    
    uint count, stride, i;
    
    _Spheres.GetDimensions(count, stride);
    for (i = 0; i < count; i++) {
        IntersectSphere(ray, bestHit, _Spheres[i]);     
    }
    
    _MeshObjects.GetDimensions(count, stride);
    for (i = 0; i < count; i++) {
        IntersectMeshObject(ray, bestHit, _MeshObjects[i]);     
    }
    
    return bestHit;
}
//_SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz;
float2 GetSampleAngle(float2 uv) {
    float value = _NoiseTexture.SampleLevel(sampler_NoiseTexture, float2(uv.xy * (_Time % 100)), 0).x;
    float angle = (value * 360) * PI / 180.0;
    return float2(cos(angle), sin(angle));
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID) {
    _Pixel = id.xy;
    
 //   float4 noise = mod(_NoiseTexture[id.xy * (_FrameCount % 100)] * GOLDEN_RATIO, 1);
    
    uint width, height;
    Result.GetDimensions(width, height);
    float2 uv = float2((id.xy + _PixelOffset) / float2(width, height) * 2.0f - 1.0f);
    
    Ray ray = CreateCameraRay(uv);
    
    // Trace and shade
    float3 result = float3(0, 0, 0);
    for (int i = 0; i < _ReflectionCount; i++) {
        RayHit hit = Trace(ray);
        result += ray.energy * Shade(ray, hit);
        if (!any(ray.energy))
            break;
    }
    
    Result[id.xy] = float4(result, 1);
}
